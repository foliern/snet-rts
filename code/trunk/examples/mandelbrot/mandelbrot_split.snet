<metadata>
  <boxdefault>
    <interface value="SAC4SNet" />
    <SACmodule>boxes</SACmodule>
  </boxdefault>
</metadata>

net mandelbrot_split
{
  box schedule( (<tasks>) -> (<tasks>, <num>)
                          |  (<tasks>, <num>, <first>));

  box genSubPlane( (params, <tasks>, <num>) -> (plane, <tasks>, <num>));

  box escTime( (plane) -> (ts));
  box escVals( (plane) -> (vs));
  box normItCount( (ts, vs) -> (nvs));
  box toRGB( (nvs) -> (rgb_img));

  net merge ({nvs, <tasks>, <num>, <first>} | {nvs, <tasks>, <num>} -> {nvs, <done>}) {
    box init( (nvs, <tasks>, <num>, <first>)
              -> (res, tracker) |
                 (nvs, <done>));
    box merge( (nvs, <tasks>, <num>, res, tracker)
               -> (res, tracker) |
                  (nvs, <done>) );
  } connect (init | [])
              .. ( [|{res,tracker},{nvs,<tasks>,<num>}|]
                   .. ( merge | [] )
                 )**{<done>};

} connect schedule
       .. [{<num>, <nodes>} -> {<num>, <instance = num % nodes>}]
       .. (genSubPlane
       .. [{plane} -> {plane, <T>}; {plane, <V>}]
       .. ( ([{<T>} -> {}] .. escTime)
            |
            ([{<V>} -> {}] .. escVals))
       .. [|{ts}, {vs}|]*{ts,vs}
       .. normItCount)!!@<instance>
       .. merge
       .. [{nvs} -> {}];



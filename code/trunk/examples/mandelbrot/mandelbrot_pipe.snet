<metadata>
  <boxdefault>
    <interface value="SAC4SNet" />
    <SACmodule>boxes</SACmodule>
  </boxdefault>
</metadata>

net mandelbrot_pipe
{
  box genPlane( (params) -> (plane));
  box escTime( (plane) -> (ts));
  box escVals( (plane) -> (vs));
  box normItCount( (ts, vs) -> (nvs));
  box toRGB( (nvs) -> (rgb_img));

  net show {
    box createDisplay( -> (disp));
    box displayImg( (disp, rgb_img) -> (disp) | (<fin>));
  } connect (createDisplay | [])
         .. ( [|{rgb_img},{disp}|]
              .. ( displayImg | [] )
            )*{<fin>};
}
connect genPlane@0
     .. [{plane} -> {plane, <T>}; {plane, <V>}]@0
     .. ( ([{<T>} -> {}] .. escTime)@1
          |
          ([{<V>} -> {}] .. escVals)@0)@0
     .. [|{ts}, {vs}|]*{ts,vs}@1
     .. normItCount@1
     .. toRGB@0
     .. show@1;

